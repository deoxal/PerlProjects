#!/usr/bin/env perl
#cito M:755 O:0 G:0 T:/usr/bin/apt-download-deps
#------------------------------------------------------------------------------
# Project Name      - PerlProjects/source/apt-download-deps
# Started On        - Sat  4 Jan 21:55:31 GMT 2020
# Last Change       - Fri 31 Jan 22:37:11 GMT 2020
# Author E-Mail     - terminalforlife@yahoo.com
# Author GitHub     - https://github.com/terminalforlife
#------------------------------------------------------------------------------
# Dependencies:
#
#   libapt-pkg-perl (>= 0.1.29build7)
#   libtfl-perl (>= 2020-01-03)
#   perl (>= 5.22.1-9)
#----------------------------------------------------------------------------------

use strict;
use warnings;
use autodie;
use TFL 'Err', 'DepChk';
use AptPkg::Cache;

my $CurVer = "2020-01-31";

sub Usage{
	my $Info = qq{            @{[uc($TFL::PROGNAME)]} ($CurVer)
		            $TFL::AUTHOR

		            Download dependencies for one or more Debian packages.

		SYNTAX:     $TFL::PROGNAME [OPTS] [PKG_1 [PKG_2] ...]

		OPTS:       --help|-h|-?            - Display this help information.
		            --version|-v            - Output the version datestamp.
		            --include-base|-b       - Also download base package(s).

		NOTE:       This program, written in Perl, is ideal for situations in
		            which the user wishes to transfer to or store packages on
		            another machine for later offline use.
	};

	print($Info =~ tr/\t//dr)
}

my $BaseToo = 'false';

while (defined($ARGV[0])){
	if ($ARGV[0] =~ '^(--help|-h|-\?)$'){
		Usage(1); exit(0)
	}elsif ($ARGV[0] =~ '^(--version|-v)$'){
		print("$CurVer\n"); exit(0)
	}elsif ($ARGV[0] =~ '^(--include-base|-b)$'){
		$BaseToo = 'true'
	}elsif ($ARGV[0] =~ '^-'){
		Err(1, "Incorrect argument(s) specified.")
	}else{
		last
	}

	shift()
}

Err(1, 'At least one package is required.') if scalar(@ARGV) == 0;

my $Cache = AptPkg::Cache->new();

my $MissingPKG = '';
sub FetchDeps{
	my $Get = $Cache->get($_[0]);
	my $Vers = $Get->{'VersionList'};

	unless (defined($Vers)){
		Err(0, "Package '$_[0]' not found.");
		$MissingPKG = 'true';
		return(0)
	}

	my $Deps = $Vers->[0]->{'DependsList'};

	my @Packages;

	my $Iter = 0;
	while ($Iter < scalar(@{$Deps})){
		my $Dep = $Deps->[$Iter++];

		next unless $Dep->{'DepType'} eq 'Depends';

		push(@Packages, $Dep->{'TargetPkg'}->{'Name'})
	}

	return(@Packages)
}

DepChk('apt-get');

my @ToDL;
while (scalar(@ARGV) > 0){
	push(@ToDL, FetchDeps($ARGV[0]));
	if ($BaseToo eq 'true'){
		system("apt-get download $ARGV[0] @ToDL") unless $MissingPKG eq 'true';
	}else{
		system("apt-get download @ToDL") unless $MissingPKG eq 'true';
	}

	shift()
}
