#!/usr/bin/env perl
#cito M:755 O:0 G:0 T:/usr/bin/apt-download-deps
#------------------------------------------------------------------------------
# Project Name      - PerlProjects/source/apt-download-deps
# Started On        - Sat  4 Jan 21:55:31 GMT 2020
# Last Change       - Mon 11 Jan 16:17:52 GMT 2021
# Author E-Mail     - terminalforlife@yahoo.com
# Author GitHub     - https://github.com/terminalforlife
#------------------------------------------------------------------------------
# Dependencies:
#
#   libapt-pkg-perl (>= 0.1.29build7)
#   perl (>= 5.22.1-9)
#------------------------------------------------------------------------------

use strict;
use warnings;
use autodie;
use AptPkg::Cache;

my $CurVer = '2021-01-11';
my $Progrm = 'apt-download-deps';

sub Usage {
	print(qq{Usage: $Progrm [OPTS] [PKG_1 [PKG_2] ...]

		  -h, --help               - Display this help information.
		  -v, --version            - Output the version datestamp.
		  -b, --include-base       - Also download base package(s).
	} =~ tr/\t//dr)
}

my $BaseToo = 'false';

while (defined($ARGV[0])) {
	if ($ARGV[0] =~ '^(--help|-h|-\?)$') {
		Usage(1); exit(0)
	} elsif ($ARGV[0] =~ '^(--version|-v)$') {
		print("$CurVer\n"); exit(0)
	} elsif ($ARGV[0] =~ '^(--include-base|-b)$') {
		$BaseToo = 'true'
	} elsif ($ARGV[0] =~ '^-') {
		die("Incorrect argument(s) specified")
	} else {
		last
	}

	shift()
}

scalar(@ARGV) == 0 and die('At least one package is required');

my $Cache = AptPkg::Cache->new();

my $MissingPKG = '';
sub FetchDeps{
	my $Get = $Cache->get($_[0]);
	my $Vers = $Get->{'VersionList'};

	unless (defined($Vers)){
		warn("Package '$_[0]' not found");
		$MissingPKG = 'true';
		return(0)
	}

	my $Deps = $Vers->[0]->{'DependsList'};

	my @Packages;

	my $Iter = 0;
	while ($Iter < scalar(@{$Deps})){
		my $Dep = $Deps->[$Iter++];

		next unless $Dep->{'DepType'} eq 'Depends';

		push(@Packages, $Dep->{'TargetPkg'}->{'Name'})
	}

	return(@Packages)
}

my @Executables;
foreach my $Dir (split(':', $ENV{'PATH'})) {
	-d $Dir or next;
	opendir(my $DH, $Dir) or next;
	push(@Executables, readdir($DH));
	closedir($DH);
}

grep({$_ eq 'apt-get'} @Executables) or die("Dependency 'apt-get' not met");

my @ToDL;
while (scalar(@ARGV) > 0){
	push(@ToDL, FetchDeps($ARGV[0]));
	if ($BaseToo eq 'true'){
		system("apt-get download $ARGV[0] @ToDL") unless $MissingPKG eq 'true';
	}else{
		system("apt-get download @ToDL") unless $MissingPKG eq 'true';
	}

	shift()
}
