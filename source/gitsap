#!/usr/bin/env perl
#cito M:755 O:0 G:0 T:/usr/bin/gitsap
#----------------------------------------------------------------------------------
# Project Name      - PerlProjects/source/gitsap
# Started On        - Mon  6 Jan 23:46:07 GMT 2020
# Last Change       - Thu 16 Jan 22:36:15 GMT 2020
# Author E-Mail     - terminalforlife@yahoo.com
# Author GitHub     - https://github.com/terminalforlife
#----------------------------------------------------------------------------------
# This project is based on a script I wrote in shell (Bash) called `gitsa` which
# did the same thing but on a simpler and slower scale.
#
# Dependencies:
#
#   libfile-listing-perl (>= 6.04-1)
#   libgit-repository-perl (>= 1.317-1)
#   libnumber-bytes-human-perl (>= 0.09-1)
#   libtfl-perl (>= 2020-01-08)
#----------------------------------------------------------------------------------

use strict;
use warnings;
use autodie;
use TFL 'FErr', 'Boolean';
use File::Find;
use Git::Repository;
use Term::ANSIColor 'color', 'colorvalid';

no warnings 'uninitialized', 'File::Find';

my $CurVer = "2020-01-16";

sub Usage{
	print(qq{            @{[uc($TFL::PROGNAME)]} ($CurVer)
		            $TFL::AUTHOR

		            Display an elegant overview of your git repositories.

		SYNTAX:     $TFL::PROGNAME [OPTS] [REGEX]

		OPTS:       --help|-h|-?            - Displays this help information.
		            --version|-v            - Output only the version datestamp.
		            --ignore-case|-i        - Case-insensitive search for REGEX.
		            --alt-path|-P PATH      - Override value in config with PATH.
		            --config PATH           - Override default file with PATH.
		            --force-reset           - Force regeneration of user's config.
		            --parseable             - Parseable output, similar to raw.

		NOTE:       Signifying the end of OPTS with '--' is supported.

		            Where REGEX is a REGEX string to use to filter repositories by
		            the full path, relative to the 'Target' location.

		CONFIG:     A user configuration file (see below) will be generated when
		            the program is executed without such a file existing.

		            Once the 'config' file has been generated, adjust as needed.

		FILE:       ~/.config/$TFL::PROGNAME/config
	} =~ tr/\t//dr)
}

my (
	$ShowBranch, $SolidTarget, $Parseable, $Compact, $ShowSize,
	$DoCase, $Target, $ForceReset, %ColorHash, $OmitClean
);

my $MainDir = "$ENV{HOME}/.config/$TFL::PROGNAME";
my $ConfigFile = "$MainDir/config";

mkdir($MainDir) unless -d $MainDir;

while (defined($ARGV[0])){
	if($ARGV[0] =~ /^--$/){
		last # <-- End of option processing.
	}elsif ($ARGV[0] =~ '^(--help|-h|-\?)$'){
		Usage(1); exit(0)
	}elsif ($ARGV[0] =~ '^(--version|-v)$'){
		print("$CurVer\n"); exit(0)
	}elsif($ARGV[0] =~ '^(--ignore-case|-i)$'){
		$DoCase = 'i'
	}elsif($ARGV[0] =~ '^(--alt-path|-P)$'){
		shift(@ARGV);

		FErr(1, __LINE__, "Option '--alt-path|-P' requires a PATH.")
			unless length($ARGV[0]) > 0;

		$Target = $ARGV[0] =~ s/\/$//r;
		$SolidTarget = 'true'
	}elsif($ARGV[0] =~ '^(--config|-c)$'){
		shift(@ARGV);

		unless (length($ARGV[0]) > 0){
			FErr(1, __LINE__, "Option '--config|-c' requires a PATH.")
		}elsif (not -f -r $ARGV[0]){
			FErr(1, __LINE__, "Alternative configuration file missing or inaccessible.")
		}

		$ConfigFile = $ARGV[0]
	}elsif($ARGV[0] eq '--parseable'){
		$Parseable = 'true';
	}elsif($ARGV[0] =~ '^(--force-reset|-F)$'){
		$ForceReset = 'true'
	}elsif($ARGV[0] =~ '^-'){
		FErr(1, __LINE__, "Incorrect option(s) specified.")
	}else{
		last
	}

	shift(@ARGV)
}

my $RegexStr = $ARGV[0] if defined($ARGV[0]);

if (scalar(@ARGV) > 1){
	FErr(1, __LINE__, "Too many arguments -- see: $TFL::PROGNAME -h")
}elsif ($DoCase eq 'i' and scalar(@ARGV) == 0){
	FErr(1, __LINE__, "Option '--ignore-case|-i' used, but no REGEX provided.")
}

my %Index = (
	'Sets' => [
		'Compact',                  # Keep each result on one line.
		'OmitClean',                # Don't display repositories which are clean.
		'Parseable',                # Raw-like data for use by scripts, etc.
		'RegexStr',                 # REGEX to filter results by repository path.
		'ShowBranch',               # Currently-active branch of each repository.
		'ShowSize',                 # Total disk space used by each repository.
		'Target'                    # Directory in which to look for repositories.
	],

	'CKeys' => [
		'Color_Repo',               # Base name of each repository path.
		'Color_RepoDir',            # Directory name of each repository path.
		'Color_Status',             # Status string for each repository.
		'Color_Symbol'              # Symbol assigned to each category of status.
	],

	'Syms' => {
		'Char_Clean' => '≎',        # When a repository needs nothing done.
		'Char_Fix' => '⮂',          # When conflicts needing fixed are found.
		'Char_Initial' => '≔',      # Freshly-created repository yet to commit.
		'Char_Leads' => '∾',        # Repository leads by X amount of commits.
		'Char_New' => '≺',          # X amount of new, unstaged files were found.
		'Char_Removed' => '⊀',      # X amount of files were unexpectedly removed.
		'Char_Uncommitted' => '≍',  # Staged modifications yet to be committed.
		'Char_Unknown' => '!',      # When the status is unrecognised by `gitsap`.
		'Char_Unstaged' => '≭'      # Changes in the repository yet to be staged.
	}
);

my $ValidKeysTTL = @{$Index{'Sets'}} + @{$Index{'CKeys'}} + keys(%{$Index{'Syms'}});

unlink($ConfigFile) if $ForceReset eq 'true';

if (-f -r -T $ConfigFile){
	open(my $FH, '<', $ConfigFile);
	my @Data = <$FH>;
	close($FH);

	my $TTL = 0;
	foreach (@Data){
		next if length($_) <= 1 or $_ =~ '^#';
		chomp(my ($Key, $Value) = split('=', $_));

		$TTL++;

		if ($Key eq 'Target'){
			FErr(1, __LINE__, "Key 'Target' not set -- see: $TFL::PROGNAME -h")
				if $Value eq 'NULL';

			$Target = $Value =~ s/\/$//r unless $SolidTarget eq 'true'
		}elsif ($Key eq 'RegexStr'){
			$RegexStr = $Value unless length($Value) == 0
		}elsif ($Key eq 'ShowBranch'){
			Boolean($Value);
			$ShowBranch = $Value
		}elsif ($Key eq 'ShowSize'){
			Boolean($Value);
			$ShowSize = $Value
		}elsif ($Key eq 'Parseable'){
			Boolean($Value);
			$Parseable = $Value unless $Parseable eq 'true'
		}elsif ($Key eq 'OmitClean'){
			Boolean($Value);
			$OmitClean = $Value
		}elsif ($Key =~ '^Color_.*'){
			FErr(1, __LINE__, "Invalid color '$Value' detected.")
				unless colorvalid($Value);

			$ColorHash{$Key} = $Value =~ tr/['"]//dr
		}elsif ($Key =~ '^Color_.*'){
			FErr(1, __LINE__, "Invalid 'Color_*' key detected.")
				unless grep($Key, @{$Index{'CKeys'}});

			$ColorHash{$Key} = $Value =~ tr/['"]//dr
		}elsif ($Key =~ '^Char_.*'){
			if (not grep($Key, keys(%{$Index{'Syms'}}))){
				FErr(1, __LINE__, "Invalid 'Char_*' key detected.")
			}elsif (length($Value) > 1){
				FErr(1, __LINE__, "Invalid 'Char_*' value detected.")
			}elsif (length($Value) == 0){
				next
			}

			${$Index{'Syms'}}{$Key} = $Value =~ tr/['"]//dr
		}elsif ($Key eq 'Compact'){
			Boolean($Value);
			$Compact = $Value
		}else{
			FErr(1, __LINE__, "Invalid key '$Key' detected.")
		}
	}

	FErr(1, __LINE__, 'One or more keys are missing.') unless $TTL == $ValidKeysTTL
}else{
	open(my $FH, '>', $ConfigFile);

	print($FH qq{# User configuration file for `$TFL::PROGNAME` ($CurVer)

		# Specify the location (file or directory) for $TFL::PROGNAME to use.
		Target=NULL

		# Display the total human-readable size of each repository.
		ShowSize=false

		# Whether or not to display the repository's current branch.
		ShowBranch=false

		# If enabled, all clean repositories will be ommitted from the results.
		OmitClean=false

		# If set to a value, it will always be used as REGEX. This could be handy
		# if you want to always view certain repositories. This key, if used, will
		# override any REGEX string otherwise set.
		RegexStr=

		# Display text in an easy-to-parse way, overriding pretty keys shown below.
		# If enabled, all symbols will be removed.
		Parseable=false

		# Display each repository's symbol, name, and status on a single line.
		Compact=true

		# Optionally assign your own character to each type of status. The defaults
		# will be used if these keys' values are left empty.
		Char_Clean=
		Char_Fix=
		Char_Initial=
		Char_Leads=
		Char_New=
		Char_Removed=
		Char_Uncommitted=
		Char_Unknown=
		Char_Unstaged=

		# Various color keys and their values. To disable color support, simply
		# set each of these to null, by removing all of their values.
		Color_Repo=red
		Color_RepoDir=red
		Color_Status=bright_white
		Color_Symbol=bright_red
	} =~ tr/\t//dr);

	close($FH);
	printf("File '~/.config/$TFL::PROGNAME/config' generated -- please restart.\n");
	exit(0)
}

# Handle user-provided environment variables.
foreach (keys(%ENV)){
	$Target = $Target =~ s|\$$_|$ENV{$_}|gr if exists($ENV{$_})
}

FErr(1, __LINE__, "Target directory not provided.") unless length($Target) > 0;
FErr(1, __LINE__, "Target directory not found.") unless -d $Target;

# Recursive search for files in `Target` per `Conditions()`.
my @FileArr;
find(
	sub {
		my $Name = $File::Find::name;

		# Avoids error via missing argument in below `sprintf()` call, this happens
		# when the user specifies the `--alt-path|-P` option.
		#return() if length($Name) == 1;

		return unless ($Name =~ '(?:.*/)(.*)')[0] eq '.git';
		return unless -d -r -x $Name;
		my $NewName = $Name =~ s/^$Target\///r;

		push(@FileArr, ($NewName =~ '(.*)(?:/\.git$)')[0])
	},

	$Target
);

my (@Results, $Result);
foreach (@FileArr){
	my $Path = "$Target/$_";
	my $ShowPath = ($Path =~ "(?:$Target/)(.*)")[0];

	# Look for user-provided REGEX in line, if one is given.
	if (length($RegexStr) > 0){
		if ($DoCase eq 'i'){
			next unless m{$RegexStr}i
		}else{
			next unless m{$RegexStr}
		}
	}

	my $Git = Git::Repository->new('work_tree' => $Path);

	# Default values, in-case something goes wrong.
	my $Branch = "Unknown branch ";
	my $Sym = ${$Index{'Syms'}}{'unknown'};
	my $Desc = '...';

	chomp(my @Status = $Git->run('status'));

	FErr(1, __LINE__, "Unable to acquire repository information.")
		unless -f "$Path/.git/HEAD";

	unless ($ShowBranch eq 'false'){
		open(my $FH, '<', "$Path/.git/HEAD");
		$Branch = (<$FH> =~ '(?:.*/)(.*)')[0];
		close($FH);
	}

	#TODO: Combine ordered loop functionality into the one loop.

	# Loops in special order:

	foreach (@Status){
		chomp(my @Arr = split(' ', $_));

		($Sym, $Desc) = (${$Index{'Syms'}}{'Char_Initial'}, 'has no commits, yet.')
			if "$Arr[0]$Arr[1]" eq 'Initialcommit';
	}

	foreach (@Status){
		chomp(my @Arr = split(' ', $_));

		($Sym, $Desc) = (${$Index{'Syms'}}{'Char_Fix'}, 'has conflict(s).')
			if "$Arr[0]$Arr[1]$Arr[2]" eq '(fixconflictsand'
	}

	foreach (@Status){
		chomp(my @Arr = split(' ', $_));

		my @CommitsRaw = $Git->run('log', '--format=oneline');
		my $Commits = scalar(@CommitsRaw);

		($Sym, $Desc) = (${$Index{'Syms'}}{'Char_Clean'}, "is $Commits commit(s) clean.")
			if "$Arr[0]$Arr[1]$Arr[2]" eq 'nothingtocommit,';
	}

	foreach (@Status){
		chomp(my @Arr = split(' ', $_));

		my $Found = 'true' if "$Arr[0]$Arr[1]$Arr[3]" eq 'Yourbranchahead';

		if ($Found eq 'true'){
			my $Leads = $Arr[scalar(@Arr) - 2];
			($Sym, $Desc) = (${$Index{'Syms'}}{'Char_Leads'}, "leads by $Leads commit(s).")
		}
	}

	foreach (@Status){
		chomp(my @Arr = split(' ', $_));

		my $Found = 'true' if "$Arr[0]$Arr[1]" eq 'Untrackedfiles:';

		my $FilesTTL = 0;
		if ($Found eq 'true'){
			my @Data = $Git->run('status', '--short');

			foreach (@Data){
				$FilesTTL++ if $_ =~ '^?? '
			}

			($Sym, $Desc) = (${$Index{'Syms'}}{'Char_New'}, "has $FilesTTL new file(s).")
		}
	}

	foreach (@Status){
		chomp(my @Arr = split(' ', $_));

		my $Found = 'true' if $Arr[0] eq 'modified:';

		if ($Found eq 'true'){
			my @Data = $Git->run('diff', '--shortstat');

			my $FilesTTL;
			foreach (@Data){
				$FilesTTL = (split(' ', $_))[0] and last
			}

			($Sym, $Desc) = (${$Index{'Syms'}}{'Char_Uncommitted'}, "has $FilesTTL modified file(s).")
		}
	}

	foreach (@Status){
		chomp(my @Arr = split(' ', $_));

		($Sym, $Desc) = (${$Index{'Syms'}}{'Char_Unstaged'}, "has changes to commit.")
			if "$Arr[0]$Arr[1]$Arr[2]$Arr[3]" eq 'Changestobecommitted:'
	}

	# End of specially-ordered loops.

	my $DirName = ($ShowPath =~ '(.*)(?:/.*)')[0];

	# Addresses repository displaying incorrectly if there's only one directory in
	# which repositories reside. Since `$Target` is omitted, `$DirName` would be
	# empty, in that situation, hence this workaround.
	$DirName = $DirName . '/' if length($DirName) > 0;

	my $BaseName = ($ShowPath =~ '(?:.*/)?(.*)')[0];

	$Desc = "Branch '$Branch' $Desc" if $ShowBranch eq 'true';

	if ($ShowSize eq 'true'){
		use Number::Bytes::Human 'format_bytes';

		my $Bytes = 0;

		find(
			sub {
				# Tally up the bytes for each file found.
				$Bytes += (stat($File::Find::name))[7]
			},

			$Path
		);

		my $SansPeriod = substr($Desc, 0, length($Desc) - 1);
		$Desc = sprintf("%s, taking up %s.", $SansPeriod, format_bytes($Bytes))
	}

	if ($Parseable eq 'true'){
		$Result = sprintf("%s%s~~~%s", $DirName, $BaseName, $Desc)
	}elsif ($Compact eq 'true'){
		$Result = sprintf(
			color($ColorHash{'Color_Symbol'}) . "  %s  " . color('reset') .
			color($ColorHash{'Color_RepoDir'}) . "%s" . color('reset') .
			color($ColorHash{'Color_Repo'}) . "%s " . color('reset') .
			color($ColorHash{'Color_Status'}) . "%s" . color('reset'),
			$Sym, $DirName, $BaseName, $Desc
		)
	}else{
		$Result = sprintf(
			color($ColorHash{'Color_Symbol'}) . "  %s  " . color('reset') .
			color($ColorHash{'Color_RepoDir'}) . "%s" . color('reset') .
			color($ColorHash{'Color_Repo'}) . "%s\n" . color('reset') .
			color($ColorHash{'Color_Status'}) . "     %s" . color('reset'),
			$Sym, $DirName, $BaseName, $Desc
		)
	}

	next if $OmitClean eq 'true' and $Desc =~ 'commit\(s\) clean';

	push(@Results, $Result)
}

if (scalar(@Results) > 0){
	my $Count;
	foreach (@Results){
		print("$_\n");
		print("\n") unless ++$Count == scalar(@Results)
			or $Compact eq 'true' or $Parseable eq 'true'
	}
}else{
	# If all repositories are clean and OmitClean is enabled, this:
	print(STDERR "Uh-oh! ...nothing to show.\n") and exit(2)
}
